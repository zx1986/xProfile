# Custom environment variables
if [ -f ~/.env ]; then
  while IFS= read -r line; do
    if [[ ! "$line" =~ ^\s*# && -n "$line" ]]; then
      export "$line"
    fi
  done < ~/.env
fi

# asdf
{{ if eq .chezmoi.os "darwin" -}}
if [ $commands[asdf] ]; then
  asdf() {
    unfunction "$0"
    source $(brew --prefix asdf)/asdf.sh
    $0 "$@"
  }
fi
{{ else -}}
if [ -f "$HOME/.asdf/asdf.sh" ]; then
  . "$HOME/.asdf/asdf.sh"
  fpath=(${ASDF_DIR}/completions $fpath)
fi
{{ end -}}

# Atuin
# eval "$(atuin init zsh)"

# d
function d () {
  if [[ -n $1 ]]; then
    dirs "$@"
  else
    dirs -v | head -10
  fi
}
compdef _dirs d

# Git
function gi() { curl -L -s https://www.gitignore.io/api/$@ ;}

# AWS
if [ $commands[awless] ]; then
  awless() {
    unfunction "$0"
    source <(awless completion zsh)
    $0 "$@"
  }
fi

function agp() {
  echo $AWS_PROFILE
}

function asp() {
  if [[ -z "$1" ]]; then
    unset AWS_DEFAULT_PROFILE AWS_PROFILE AWS_EB_PROFILE
    echo AWS profile cleared.
    return
  fi

  local -a available_profiles
  available_profiles=($(aws_profiles))
  if [[ -z "${available_profiles[(r)$1]}" ]]; then
    echo "${fg[red]}Profile '$1' not found in '${AWS_CONFIG_FILE:-$HOME/.aws/config}'" >&2
    echo "Available profiles: ${(j:, :)available_profiles:-no profiles found}${reset_color}" >&2
    return 1
  fi

  export AWS_DEFAULT_PROFILE=$1
  export AWS_PROFILE=$1
  export AWS_EB_PROFILE=$1
}

function aws_profiles() {
  [[ -r "${AWS_CONFIG_FILE:-$HOME/.aws/config}" ]] || return 1
  grep '\[profile' "${AWS_CONFIG_FILE:-$HOME/.aws/config}"|sed -e 's/.*profile \([a-zA-Z0-9_\.-]*\).*/\1/'
}

function _aws_profiles() {
  reply=($(aws_profiles))
}

compctl -K _aws_profiles asp

# fzf
[ -f ~/.fzf.zsh ] && source ~/.fzf.zsh

# z
[ -f /usr/local/etc/profile.d/z.sh ] && source /usr/local/etc/profile.d/z.sh

# Kubernetes
kcdebug() { kubectl run -i --rm --tty debug --image=alpine --restart=Never -- sh }
if [ $commands[kubectl] ]; then
  kubectl() {
    unfunction "$0"
    source <(kubectl completion zsh)
    $0 "$@"
  }
fi

# kube-ps1: show current context in RPROMPT
# The plugin is sourced earlier via omz-kube-ps1.plugin.zsh.
# We register a precmd hook here (after Prezto) so that RPROMPT gets set
# on every prompt draw, overriding whatever Prezto's theme puts there.
if (( $+functions[kube_ps1] )); then
  _kube_ps1_rprompt() {
    RPROMPT="$(kube_ps1)"
  }
  autoload -Uz add-zsh-hook
  add-zsh-hook precmd _kube_ps1_rprompt
fi

# Helm
if [ $commands[helm] ]; then
  helm() {
    unfunction "$0"
    source <(helm completion zsh)
    $0 "$@"
  }
fi

{{ if eq .chezmoi.os "darwin" -}}
# iTerm
[ -f ~/.iterm2_shell_integration.zsh ] && source ~/.iterm2_shell_integration.zsh
{{ end -}}

# Custom Alias
[ -f ~/.aliases ] && source ~/.aliases
